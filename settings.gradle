pluginManagement {
    repositories {
        gradlePluginPortal()
        mavenCentral()
        google()
    }
    plugins {
        id "${quarkusPluginId}" version "${quarkusPluginVersion}"
        id "io.quarkus.extension" version "${quarkusPluginVersion}"
    }
}

rootProject.name = 'positizing'

List<String> modules = [
        'detector', 'android', 'watch', 'phone', 'server'
]
modules.each { m ->
    include m
    ProjectDescriptor p = project(":$m")
    File namedBuildscript = new File(p.projectDir, "${m}.gradle")
    if (namedBuildscript.exists()) {
        p.buildFileName = namedBuildscript.name
    }
}

List<String> pluginModules = [
        'plugin-tools', 'google-docs'
]
pluginModules.each { m ->
    include m
    ProjectDescriptor p = project(":$m")
        File dir = file("plugins/$m")
        File namedBuildscript = new File(dir, "${m}.gradle")
        if (namedBuildscript.exists()) {
            p.buildFileName = namedBuildscript.name
        }
        p.projectDir = dir
}

dependencyResolutionManagement {
    versionCatalogs {
        libs {
            // we define all our version information in .toml file and access as `libs.xapi.whatever` in a typesafe manner
            from(files('gradle/versions.toml'))
        }
    }
}
include 'desktop'

